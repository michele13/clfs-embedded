<?xml version='1.0'?> <!-- -*- nxml -*- vim: set foldlevel=2: -->
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:sf="http://sourceforge.net/"
  xmlns:dyn="http://exslt.org/dynamic"
  xmlns:saxon="http://icl.com/saxon"
  exclude-result-prefixes="sf"
  version='1.0'>

<xsl:output omit-xml-declaration="yes"/>

<xsl:param name="get"/>
<xsl:param name="VERSION" select="string(document('')//sf:Version[1])"/>
<xsl:param name="Tag" select="concat('V',translate(string(document('')//sf:Version[1]),'.',''))"/>
<xsl:param name="DistroTitle" select="string(document('')//sf:Branch[1])"/>
<xsl:param name="sf-relid" select="0"/>

<xsl:param name="DistroName">docbook-xsl</xsl:param>
<xsl:param name="PreviousRelease">1.72.0</xsl:param>
<xsl:param name="PreviousReleaseRevision">6549</xsl:param>
<xsl:param name="Revision">$Revision$</xsl:param>
<xsl:param name="VersionFileURL">$URL: https://docbook.svn.sourceforge.net/svnroot/docbook/trunk/xsl/VERSION $</xsl:param>

<xsl:strip-space elements="sf:*"/>

<sf:project>
  <sf:Project>DocBook</sf:Project>
  <sf:Branch>XSL Stylesheets</sf:Branch>
  <sf:Version>1.73.2</sf:Version>
<!--
  <sf:License>MIT/X Consortium License</sf:License>
-->
  <sf:Release-Focus>
<!-- * Documentation -->
<!-- * Code cleanup -->
<!-- * Minor feature enhancements -->
<!-- * Major feature enhancements -->
Minor bugfixes
<!-- * Major bugfixes -->
<!-- * Minor security fixes -->
<!-- * Major security fixes -->
  </sf:Release-Focus>
  <sf:Home-Page-URL>http://sourceforge.net/projects/docbook/</sf:Home-Page-URL>
  <sf:Gzipped-Tar-URL>http://prdownloads.sourceforge.net/docbook/{DISTRONAME-VERSION}.tar.gz?download</sf:Gzipped-Tar-URL>
  <sf:Zipped-Tar-URL>http://prdownloads.sourceforge.net/docbook/{DISTRONAME-VERSION}.zip?download</sf:Zipped-Tar-URL>
  <sf:Bzipped-Tar-URL>http://prdownloads.sourceforge.net/docbook/{DISTRONAME-VERSION}.bz2?download</sf:Bzipped-Tar-URL>
  <sf:Changelog-URL>http://sourceforge.net/project/shownotes.php?release_id={SFRELID}</sf:Changelog-URL>
  <sf:CVS-URL>http://docbook.svn.sourceforge.net/viewvc/docbook/</sf:CVS-URL>
  <sf:Mailing-List-URL>http://lists.oasis-open.org/archives/docbook-apps/</sf:Mailing-List-URL>
  <sf:Changes>This is a bug-fix update to the 1.73.1 release.
</sf:Changes>
</sf:project>

<xsl:template match="/" priority="-100">
  <xsl:choose>
    <xsl:when test="$get = 'Tag'">
      <xsl:value-of select="$Tag"/>
    </xsl:when>
    <xsl:when test="$get = 'PreviousRelease'">
      <xsl:value-of select="$PreviousRelease"/>
    </xsl:when>
    <xsl:when test="$get = 'PreviousReleaseRevision'">
      <xsl:value-of select="$PreviousReleaseRevision"/>
    </xsl:when>
    <xsl:when test="$get = 'DistroTitle'">
      <xsl:value-of select="$DistroTitle"/>
    </xsl:when>
    <xsl:otherwise>
      <xsl:if test="$sf-relid = 0">
        <xsl:message terminate="yes">
         <xsl:text>You must specify the sf-relid as a parameter.</xsl:text>
        </xsl:message>
      </xsl:if>
      <xsl:apply-templates select="//sf:project"/>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:template match="sf:project">
  <xsl:apply-templates/>
  <xsl:text>&#10;</xsl:text>
  <xsl:apply-templates select="sf:Changes" mode="text"/>
</xsl:template>

<xsl:template match="sf:Changes"/>

<xsl:template match="sf:Gzipped-Tar-URL|sf:Zipped-Tar-URL|sf:Bzipped-Tar-URL">
  <xsl:value-of select="local-name(.)"/>
  <xsl:text>: </xsl:text>
  <xsl:value-of select="substring-before(., '{DISTRONAME-VERSION}')"/>
  <xsl:value-of select="concat($DistroName, '-', $VERSION)"/>
  <xsl:value-of select="substring-after(., '{DISTRONAME-VERSION}')"/>
  <xsl:text>&#10;</xsl:text>
</xsl:template>

<xsl:template match="sf:Changelog-URL">
  <xsl:value-of select="local-name(.)"/>
  <xsl:text>: </xsl:text>
  <xsl:value-of select="substring-before(., '{SFRELID}')"/>
  <xsl:value-of select="$sf-relid"/>
  <xsl:value-of select="substring-after(., '{SFRELID}')"/>
  <xsl:text>&#10;</xsl:text>
</xsl:template>

<xsl:template match="sf:*">
  <xsl:value-of select="local-name(.)"/>
  <xsl:text>: </xsl:text>
  <xsl:value-of select="normalize-space(.)"/>
  <xsl:text>&#10;</xsl:text>
</xsl:template>

</xsl:stylesheet>
